plugins {
    id 'java-library'
    id 'maven-publish'
}

apply from: '../repositories.gradle'

dependencies {
    api 'org.gwtproject:gwt-dev:2.11.0',
        'org.treblereel.gwt.gwtproject.resources:gwt-resources-api:202402231'
    annotationProcessor 'org.treblereel.gwt.gwtproject.resources:gwt-resources-processor:202402231'
}

compileJava.options.sourcepath = files(processResources.destinationDir).builtBy(processResources)

task prepareJs(type: Copy, dependsOn: project.getParent().createGiacWasmJs) {
    from "${project.getParent().buildDir}/binaries/giacggb.wasm"
    into file("src/main/resources/fr/grenoble/ujf/giac")
    include "giac.wasm.js"
}

task sourcesJar(type: Jar, dependsOn: "classes") {
    archiveClassifier.set('sources')  // Updated to use 'archiveClassifier.set'
    from sourceSets.main.allSource
    from files(file("build/generated/sources/annotationProcessor/java/main/"))
}

compileJava.dependsOn prepareJs

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'giac-gwt'
            groupId = 'fr.ujf-grenoble'
            version = project.getParent().ggrev
            from components.java
            artifact sourcesJar
            pom {
                name = 'Giac for GWT'
                description = 'GWT binding for giac.wasm.js'
            }
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
